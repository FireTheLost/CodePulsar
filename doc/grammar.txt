----------------------------META-GRAMMAR-----------------------------

Key:
    | "?" : Zero Or One Times
    | "*" : Zero Or More Times
    | "+" : One Or More Times

-------------------------------GRAMMAR-------------------------------

file:
    | program EOF

program:
    | package_declaration? import_statement* namespace_declaration
    | package_declaration? import_statement* declaration*

package_declaration:
    | "package" IDENTIFIER ("::" IDENTIFIER)* ";"

import_statement:
    | "import" IDENTIFIER ("::" IDENTIFIER)* ";"

namespace_declaration:
    | "namespace" IDENTIFIER ("::" IDENTIFIER)* "{" declaration* "}"

namespace_identifier:
    | IDENTIFIER ("::" IDENTIFIER)* "::"

declaration:
    | class_declaration
    | trait_declaration
    | trait_implementation
    | enum_declaration
    | function_declaration
    | variable_declaration

access_modifier:
    | ("public" | "private")

class_declaration:
    | access_modifier? "class" IDENTIFIER ("extends" namespace_identifier? DATATYPE)? "{" declaration* "}"

trait_declaration:
    | access_modifier? "trait" IDENTIFIER "{" (function_signature ";")* "}"

trait_implementation:
    | IDENTIFIER "implements" IDENTIFIER "{" function_declaration* "}"

enum_declaration:
    | access_modifier? "enum" IDENTIFIER? "{" enum_block? "}"

enum_block:
    | IDENTIFIER ("," IDENTIFIER)* ","?

function_declaration:
    | function_signature block

function_signature:
    | access_modifier? function_type IDENTIFIER "(" parameter? ")" "->" DATATYPE

function_type:
    | ("fun" | "method" | "init" | "static" | "override" | "native")

parameter:
    | IDENTIFIER ":" DATATYPE ("," IDENTIFIER ":" DATATYPE)*

variable_declaration:
    | access_modifier? ("var" | "const") IDENTIFIER ":" namespace_identifier? DATATYPE ("=" expression)? ";"

block:
    | "{" statement* "}"

statement:
    | declaration
    | while_statement
    | if_statement
    | return_statement
    | expression_statement

while_statement:
    | "while" expression block

if_statement:
    | "if" expression block ("else" (if_statement | block))?

return_statement:
    | "return" expression? ";"

expression_statement:
    | expression ";"

expression:
    | assignment

assignment:
    | IDENTIFIER ("=" | "+=" | "-=" | "*=" | "/=" | "%=") assignment
    | logical_or

logical_or:
    | logical_and ("||" logical_and)*

logical_and:
    | equality ("&&" equality)*

equality:
    | comparison (("!=" | "==") comparison)*

comparison:
    | term ((">" | ">=" | "<" | "<=") term)*

term:
    | factor (("-" | "+") factor)*

factor:
    | unary (("/" | "*" | "%") unary)*

unary:
    | ("!" | "-" | "+") unary
    | call

call:
    | primary ("(" argument? ")")?

argument:
    | expression ("," expression)*

primary:
    | namespace_identifier? IDENTIFIER
    | DATA
    | "(" expression ")"