---------------------------------KEY---------------------------------

Key:
    | "?" : Zero Or One Times
    | "*" : Zero Or More Times
    | "+" : One Or More Times

-------------------------------GRAMMAR-------------------------------

statement:
	| if_statement
	| while_statement
	| expression_statement

if_statement:
	| "if" expression block ("else" (if_statement | block))?

while_statement:
	| "while" expression block

block:
	| "{" statement* "}"

expression_statement:
	| expression ";"

expression:
	| assignment

assignment:
	| IDENTIFIER ("=" | "+=" | "-=" | "*=" | "/=") assignment
	| logical_or

logical_or:
	| logical_and ("||" logical_and)*

logical_and:
	| equality ("&&" equality)*

equality:
	| comparison (("!=" | "==") comparison)*

comparison:
	| term ((">" | ">=" | "<" | "<=") term)*

term:
	| factor (("-" | "+") factor)*

factor:
	| unary (("/" | "*" | "%") unary)*

unary:
    | ("!" | "-") unary
    | primary

primary:
    | IDENTIFIER
    | DATA
    | "(" expression ")"